apply plugin: 'com.android.application'

def appVersion = rootProject.ext.appVersion
def supportDependencies = rootProject.ext.dependencies
def supportAnnotationProcessor = rootProject.ext.annotationProcessor

android {
    compileSdkVersion appVersion.compileSdkVersion
    buildToolsVersion appVersion.buildToolsVersion
    defaultConfig {
        applicationId "com.parting_soul.componentizationdemo"
        minSdkVersion appVersion.minSdkVersion
        targetSdkVersion appVersion.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // 在gradle文件中配置选项参数信息(用于APT传参)，必须写在defaultConfig节点下
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['content': 'hellp apt']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    supportDependencies.each {
        k, v -> implementation v
    }

    supportAnnotationProcessor.each {
        k, v -> annotationProcessor v
    }

    implementation project(path: ':base')
    implementation project(path: ':annoation')
    annotationProcessor project(':compiler')
    if (isRelease) {
        implementation project(":order")
        implementation project(":personal")
    }

}
